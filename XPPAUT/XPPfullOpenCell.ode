# Jurkat Model 
#======================================
#======PARAMETERS======================
#======================================
#======================================
# PMCA 
par Vpm=3
par Kpm=0.2
#======================================
# SOCE 
s1=0.2
Vsoce=3
Ts=15
Ke=400
#======================================
# IP3
Vdeg=6
Kdeg=0.5
par Vplc=0.1
Tp=2
#======================================
# General 
gamma=5.5
par delta=2

#======================================
# IPR 
Kf=1.6
kbeta=0.4
Kp=10
Kc=0.16
#======================================
# H 
Kh=0.168
Tmax=7.5
Kt=0.09
#======================================
# SERCA 
Vs=2
Kbar=1e-08
Ks=0.19
#======================================
# Conservation law
# ce = gamma*(Ct-c)
#======================================
#======FLUXES==========================
#======================================
#IPR flux
ma = c**4 / (Kc**4 + c**4)
B = p**2 / (Kp**2 + p**2)
A = 1 - p**2 / (Kp**2 + p**2)
ha = Kh**4 / (Kh**4 + c**4)
alpha = A*(1-ma*ha)
beta = B*ma*h
P0 = beta/(beta + kbeta*(beta + alpha))   
Jipr=Kf*P0*(ce-c)
Jh=(Kh**4 / (Kh**4 + c**4)) - h
Th=Tmax* (Kt**4 / (Kt**4 + c**4))
aux ipr=Jipr
#======================================
#SERCA flux
Jserca=(Vs*(c**2 - Kbar*ce**2))/(c**2 + Ks**2)
aux serca=Jserca
#======================================
#IP3 production
IPdeg=Vdeg*c**2/(c**2+Kdeg**2)
L=Vplc - IPdeg*p
#======================================
#PMCA flux
Jpm = (Vpm*c**2)/(c**2 + Kpm**2)
#======================================
#SOCE flux
Jsoce = Vsoce/(1+exp(s1*(ce-Ke)))
aux Ct=c + ce/gamma
#======================================
#======EQUATIONS=======================
#======================================
dc/dt=(Jipr - Jserca) + delta*(S - Jpm)
dce/dt=gamma*(Jserca - Jipr)
dh/dt=Jh/Th
dp/dt=(L)/Tp
ds/dt=(Jsoce - s)/Ts
#======================================
#======ICS=============================
#======================================
# Initial conditions
init c=0.0790,
init ce=790.334,
init h=0.93681,
init p=0
init s=0.065125
# init Aux=0
#======================================
#======XPP OPTIIONS====================
#======================================
# XPP Options
# @ TOTAL=-100
@ TOTAL=240
@ MAXSTOR=10000000
@ DT=0.01
@ XP=T
@ YP=C
@ XLO=0
@ YLO=0
@ XHI=240
@ YHI=0.4
@ METH=stiff
@ BOUNDS=3000
@ NMESH=400
#======================================
#======AUTO OPTIONS====================
#======================================
@ NTST=300  
@ NMAX=30000
@ NPR=3000
@ DSMIN=1e-10
@ DSMAX=1e-1
@ DS=1e-3
@ EPSS=1e-9 
@ EPSL=1e-9
@ EPSU=1e-7
# AUTO Plot Options
@ PARMIN=-0.1
@ PARMAX=1
@ AUTOXMIN=0
@ AUTOYMIN=0
@ AUTOXMAX=0.1
@ AUTOYMAX=0.4
@ AUTOVAR=C
